apply plugin: 'java'
apply plugin: 'maven'

group 'net.scriptgate.engine'
version '0.0.1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "https://github.com/Scriptgate/scriptgate-repo/raw/master"
    }
}

dependencies {
    /*
    When building with Gradle, resources are stored in build/resources,
    they're however not added to the classpath.
    When running Make in Intellij, the resources are correctly placed
    in the build/classes.

    workaround is to manually put resources on the classpath as a runtime dependency
     */
    runtime files('src/main/resources')
    compile group: 'net.scriptgate.engine', name: 'engine-api', version: '0.0.1-SNAPSHOT', changing: true
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.6.2'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.6.2'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, "seconds"
}

task createPom << {
    pom {
        project {
            groupId project.group
            artifactId project.name
            version project.version
            description 'POM was created with Gradle'
        }
    }.writeTo("${project.libsDir}/${project.name}-${project.version}.pom")
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourceJar
}